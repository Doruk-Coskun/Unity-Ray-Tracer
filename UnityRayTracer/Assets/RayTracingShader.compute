// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;

static const float PI = 3.14159265f;

uint _MaxRecursionDepth;

/*-----Skybox-----*/

Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;

float3 _BackgroundColor;

/*-----Camera-----*/

float4x4 _CameraToWorldMatrix;
float4x4 _CameraInverseProjectionMatrix;

/*-----Lights-----*/

float3 _DirectLightDirection;
float3 _DirectLightIntensity;

float3 _AmbientLight;

uint _PointLightCount;

struct PointLight 
{
    float3 position;
    float3 intensity;
};

StructuredBuffer<PointLight> _PointLightList;

/*-----Material-----*/

uint _MaterialCount;

struct MaterialData 
{
    float3 ambientRef;
    float3 diffuseRef;
    float3 specularRef;
    float3 mirrorRef;
    float3 transparency;
    
    float phongExponent;
    float refractionIndex;
};

StructuredBuffer<MaterialData> _MaterialList;

/*-----SceneGeometry-----*/

uint _SphereCount;
uint _MeshCount;

uint _SizeOfVertexList;
uint _SizeOfTriangleList;

struct Sphere
{
    uint materialID;
    float3 position;
    float radius;
};

struct MeshData 
{
    uint materialID;
    
    uint triangleIndexStart;
    uint triangleIndexEnd;
    uint vertexIndexStart;
};

StructuredBuffer<Sphere> _SphereList;

StructuredBuffer<float3> _VertexList;
StructuredBuffer<float3> _TriangleList;
StructuredBuffer<MeshData> _MeshDataList;

/*-----Ray & RayHit-----*/

struct Ray 
{
    float3 origin;
    float3 direction;
    float3 energy;
};

struct RayHit 
{
    float3 position;
    float distance;
    float3 normal;
    uint materialID;
};

/*--------------------------*/

Ray CreateRay(float3 origin, float3 direction) 
{
    Ray ray;
    ray.origin = origin;
    ray.direction = normalize(direction);
    ray.energy = float3(1.0f, 1.0f, 1.0f);
    return ray;
}

RayHit CreateRayHit() 
{
    RayHit rayHit;
    rayHit.position = float3(0.0f, 0.0f, 0.0f);
    rayHit.distance = 1.#INF;
    rayHit.normal = float3(0.0f, 0.0f, 0.0f);
    rayHit.materialID = 0;
    
    return rayHit;
}

Ray CreateCameraRay(float2 uv) 
{
    float3 origin = mul(_CameraToWorldMatrix, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    
    float3 direction = mul(_CameraInverseProjectionMatrix, float4(uv, 0.0f, 1.0f)).xyz;
    direction = mul(_CameraToWorldMatrix, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);
    
    return CreateRay(origin, direction);
}

void IntersectSphere(Ray ray, inout RayHit bestHit, Sphere sphere)
{
    // Calculate distance along the ray where the sphere is intersected
    float3 d = ray.origin - sphere.position;
    float p1 = -dot(ray.direction, d);
    float p2sqr = p1 * p1 - dot(d, d) + sphere.radius * sphere.radius;
    if (p2sqr < 0)
        return;
    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < bestHit.distance)
    {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = normalize(bestHit.position - sphere.position);
        bestHit.materialID = sphere.materialID;
    }
}

void IntersectTriangle(Ray ray, inout RayHit bestHit, MeshData meshData, float3 a, float3 b, float3 c) 
{        
        float3 ab = a - b;
        float3 ac = a - c;

        // Intermediate products
        float ei_hf, gf_di, dh_eg;

        ei_hf = ac.y * ray.direction.z - ray.direction.y * ac.z;
        gf_di = ray.direction.x * ac.z - ac.x * ray.direction.z;
        dh_eg = ac.x * ray.direction.y - ac.y * ray.direction.x;

        float ak_jb, jc_al, bl_kc;
        float j,k,l;

        j = a.x - ray.origin.x;
        k = a.y - ray.origin.y;
        l = a.z - ray.origin.z;

        ak_jb = ab.x * k - j * ab.y;
        jc_al = j * ab.z - ab.x * l;
        bl_kc = ab.y * l - k * ab.z;

        float M = ab.x * (ei_hf) + 
                  ab.y * (gf_di) +
                  ab.z * (dh_eg);

        float t = - (ac.z * ak_jb +
                   ac.y * jc_al +
                   ac.x * bl_kc) / M;

        // Check for intersection
        if (t <= 0) {
            //return false;
        } else {
            float gamma = (ray.direction.z * ak_jb +
                           ray.direction.y * jc_al +
                           ray.direction.x * bl_kc) / M; 

            if (gamma < 0 || gamma > 1) {
                //return false;
            } else {
                float beta = (j * ei_hf +
                              k * gf_di +
                              l * dh_eg) / M;
                if (beta < 0 || beta > 1 - gamma) {
                    //return false;
                } else {
                    // Fill out hit record
                    if (bestHit.distance > t) {
                        bestHit.distance = t;
                        float3 normal = normalize(cross((b - a), (c - a)));
                        bestHit.normal = normal;
                        bestHit.position = ray.origin + t * ray.direction;
                        bestHit.materialID = meshData.materialID;
                    }
                    //return true;
                }
            }
        }

        //return false;
}

void IntersectMesh(Ray ray, inout RayHit bestHit, MeshData meshData) 
{
    for (uint i = meshData.triangleIndexStart; i < meshData.triangleIndexEnd; i ++) 
    {
        IntersectTriangle(ray, bestHit, meshData, 
                                        _VertexList[_TriangleList[i].x + meshData.vertexIndexStart],
                                        _VertexList[_TriangleList[i].y + meshData.vertexIndexStart],
                                        _VertexList[_TriangleList[i].z + meshData.vertexIndexStart]
                                        );
    }
}

RayHit Trace(Ray ray)
{
    RayHit bestHit = CreateRayHit();
    //IntersectGroundPlane(ray, bestHit);
    
    for (uint i = 0; i < _SphereCount; i++) 
    {
        IntersectSphere(ray, bestHit, _SphereList[i]);
    }
    
    for (uint i = 0; i < _MeshCount; i++) 
    {
        IntersectMesh(ray, bestHit, _MeshDataList[i]);
    }
    
    return bestHit;
}

float3 CalculateDirectLight(RayHit hit, float3 viewVector) 
{
    float3 color = float3(0.0, 0.0, 0.0);
    
    color += saturate(dot(hit.normal, -_DirectLightDirection)) * _DirectLightIntensity * 
        _MaterialList[hit.materialID].diffuseRef;
        
    float3 h = normalize(-_DirectLightDirection + -viewVector);
    float NdotH = saturate(dot(h, hit.normal));
    //Calculate Blinn-Phong Shader
    color +=  pow(NdotH, _MaterialList[hit.materialID].phongExponent) *
        _MaterialList[hit.materialID].specularRef * _DirectLightIntensity;
    
    return color;
}

float3 CalculatePointLight(RayHit hit, uint PLIndex, float3 viewVector) 
{
    float3 color = float3(0.0, 0.0, 0.0);

    float3 toLightVector = _PointLightList[PLIndex].position - hit.position;
    float distance = length(toLightVector);
        
    Ray toLightRay = CreateRay(hit.position + hit.normal * 0.001f, normalize(toLightVector));
    RayHit shadowHit = Trace(toLightRay);
    
    if (shadowHit.distance < distance) 
    {
        return color;
    }
    else 
    {
        // Diffuse shading
        color += saturate(dot(hit.normal, toLightRay.direction)) * _PointLightList[PLIndex].intensity *
            _MaterialList[hit.materialID].diffuseRef / (distance * distance);
        
        // Specular shading
        //Calculations of half vector and 
        float3 h = normalize(toLightRay.direction + -viewVector);
        float NdotH = saturate(dot(h, hit.normal));
        //Calculate Blinn-Phong Shader
        color +=  pow(NdotH, _MaterialList[hit.materialID].phongExponent) *
            _MaterialList[hit.materialID].specularRef * _PointLightList[PLIndex].intensity / (distance * distance);
        
        
        return color;
    }
}

float3 CalculateLights(Ray ray, RayHit hit) 
{
    float3 color = float3(0.0, 0.0, 0.0);
    
    ray.origin = hit.position + hit.normal * 0.001f;
    
    color += _MaterialList[hit.materialID].ambientRef * _AmbientLight;
    
    for (uint i = 0; i < _PointLightCount; i++) 
    {
        color += CalculatePointLight(hit, i, ray.direction);
    }
    
    color += CalculateDirectLight(hit, ray.direction);
    
    return color;
}

float3 Shade(inout Ray ray, RayHit hit)
{
    if (hit.distance < 1.#INF) 
    {        
        float3 color = CalculateLights(ray, hit);
        
        if (length(_MaterialList[hit.materialID].mirrorRef) == 0 && 
                length(_MaterialList[hit.materialID].transparency) == 0) 
        {
            ray.energy = 0; 
        }
        else 
        {
            if (length(_MaterialList[hit.materialID].mirrorRef) > length(_MaterialList[hit.materialID].transparency)) 
            {
                // TODO: Calculate refraction color with a new ray and add it to the color
                
                // Ray reflects, next iteration in the main loop calculates its value
                ray.origin = hit.position + hit.normal * 0.001f;
                ray.direction = reflect(ray.direction, hit.normal);
                ray.energy *= _MaterialList[hit.materialID].mirrorRef;
            }
            else 
            {
                // TODO: Calculate reflection color with a new ray and add it to the color
                
                // Ray refracts, next iteration in the main loop calculates its value
            }
        }
        
        return color;
    }
    else 
    {
        ray.energy = 0.0f;
    
        //float theta = acos(ray.direction.y) / -PI;
        //float phi = atan2(ray.direction.x, -ray.direction.z) / -PI * 0.5f;
        //return _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz;
        return _BackgroundColor;
    }
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint width, height;
    Result.GetDimensions(width, height);
    
    float2 uv = float2((id.xy + float2(0.5f, 0.5f)) / float2(width, height) * 2.0f - 1.0f);
    
    Ray ray = CreateCameraRay(uv);
    
    float3 result = float3(0.0f, 0.0f, 0.0f);
    
    for (uint i = 0; i < _MaxRecursionDepth; i++) 
    {
        RayHit hit = Trace(ray);
        result += ray.energy * Shade(ray, hit);
        
        if (!any(ray.energy))
        {
            break;
        }
    }
    
    Result[id.xy] = float4(result, 1);
}