// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;
Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;

static const float PI = 3.14159265f;

float4x4 _CameraToWorldMatrix;
float4x4 _CameraInverseProjectionMatrix;

float4 _DirectionalLightDirection;
float4 _DirectionalLightColor;

uint _SphereCount;
uint _PointLightCount;

struct Ray 
{
    float3 origin;
    float3 direction;
    float3 energy;
};

struct RayHit 
{
    float3 position;
    float distance;
    float3 normal;
    float3 albedo;
    float3 specular;
};

struct Sphere
{
    float3 position;
    float radius;
    float3 albedo;
    float3 specular;
};

StructuredBuffer<Sphere> _Spheres;


Ray CreateRay(float3 origin, float3 direction) 
{
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    ray.energy = float3(1.0f, 1.0f, 1.0f);
    return ray;
}

RayHit CreateRayHit() 
{
    RayHit rayHit;
    rayHit.position = float3(0.0f, 0.0f, 0.0f);
    rayHit.distance = 1.#INF;
    rayHit.normal = float3(0.0f, 0.0f, 0.0f);
    rayHit.specular = float3(0.0f, 0.0f, 0.0f);
    rayHit.albedo = float3(0.0f, 0.0f, 0.0f);
    
    return rayHit;
}

Ray CreateCameraRay(float2 uv) 
{
    float3 origin = mul(_CameraToWorldMatrix, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    
    float3 direction = mul(_CameraInverseProjectionMatrix, float4(uv, 0.0f, 1.0f)).xyz;
    direction = mul(_CameraToWorldMatrix, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);
    
    return CreateRay(origin, direction);
}

void IntersectGroundPlane(Ray ray, inout RayHit bestHit)
{
    float t = -ray.origin.y / ray.direction.y;
    if (t > 0 && t < bestHit.distance)
    {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = float3(0.0f, 1.0f, 0.0f);
        bestHit.albedo = float3(0.5f, 0.5f, 0.5f);
        bestHit.specular = float3(0.5f, 0.5f, 0.5f);
    }
}

void IntersectSphere(Ray ray, inout RayHit bestHit, Sphere sphere)
{
    // Calculate distance along the ray where the sphere is intersected
    float3 d = ray.origin - sphere.position;
    float p1 = -dot(ray.direction, d);
    float p2sqr = p1 * p1 - dot(d, d) + sphere.radius * sphere.radius;
    if (p2sqr < 0)
        return;
    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < bestHit.distance)
    {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = normalize(bestHit.position - sphere.position);
        bestHit.albedo = sphere.albedo;
        bestHit.specular = sphere.specular;
    }
}

RayHit Trace(Ray ray)
{
    RayHit bestHit = CreateRayHit();
    IntersectGroundPlane(ray, bestHit);
    
    for (uint i = 0; i < _SphereCount; i++)
        IntersectSphere(ray, bestHit, _Spheres[i]);
    
    return bestHit;
}

float3 Shade(inout Ray ray, RayHit hit)
{
    if (hit.distance < 1.#INF) 
    {        
        ray.origin = hit.position + hit.normal * 0.001f;
        ray.direction = reflect(ray.direction, hit.normal);
        ray.energy *= hit.specular;
        
        bool shadow = false;
        Ray shadowRay = CreateRay(hit.position + hit.normal * 0.001f, -1 * _DirectionalLightDirection.xyz);
        RayHit shadowHit = Trace(shadowRay);
        
        if (shadowHit.distance != 1.#INF)
        {
            return float3(0.0f, 0.0f, 0.0f);
        }
        
        // TODO: Directional light color is not used.
        return saturate(dot(hit.normal, _DirectionalLightDirection.xyz) * -1) * _DirectionalLightColor.w * hit.albedo;

    }
    else 
    {
        ray.energy = 0.0f;
    
        float theta = acos(ray.direction.y) / -PI;
        float phi = atan2(ray.direction.x, -ray.direction.z) / -PI * 0.5f;
        return _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz;
    }
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint width, height;
    Result.GetDimensions(width, height);
    
    float2 uv = float2((id.xy + float2(0.5f, 0.5f)) / float2(width, height) * 2.0f - 1.0f);
    
    Ray ray = CreateCameraRay(uv);
    float3 result = float3(0.0f, 0.0f, 0.0f);
    
    for (int i = 0; i < 8; i++) 
    {
        RayHit hit = Trace(ray);
        result += ray.energy * Shade(ray, hit);
        
        if (!any(ray.energy))
        {
            break;
        }
    }
    
    Result[id.xy] = float4(result, 1);
}